<!-- vim: syntax=markdown -->

# Advent of Code

## Setup

```elixir
Mix.install([
  {:httpoison, "~> 1.8"},
  {:jason, "~> 1.2"},
  {:vega_lite, "~> 0.1.2"},
  {:kino, "~> 0.3.1"}
])

alias VegaLite, as: Vl

ExUnit.start(autorun: false)
```

```elixir
defmodule AoCInput do
  use HTTPoison.Base

  @cookie [
    File.read!("aoc.cookie")
  ]

  def get_input(day, year \\ 2021) do
    url = "https://adventofcode.com/#{year}/day/#{day}/input"

    %HTTPoison.Response{body: body, status_code: 200} = get!(url, [], hackney: [cookie: @cookie])

    body
  end
end
```

<!-- livebook:{"branch_parent_index":0} -->

## Day 1

As the submarine drops below the surface of the ocean, it automatically performs a sonar sweep of the nearby sea floor. On a small screen, the sonar sweep report (your puzzle input) appears: each line is a measurement of the sea floor depth as the sweep looks further and further away from the submarine.

For example, suppose you had the following report:

```
199
200
208
210
200
207
240
269
260
263
```

This report indicates that, scanning outward from the submarine, the sonar sweep found depths of 199, 200, 208, 210, and so on.

The first order of business is to figure out how quickly the depth increases, just so you know what you're dealing with - you never know if the keys will get carried into deeper water by an ocean current or a fish or something.

To do this, count the number of times a depth measurement increases from the previous measurement. (There is no measurement before the first measurement.) In the example above, the changes are as follows:

```
199 (N/A - no previous measurement)
200 (increased)
208 (increased)
210 (increased)
200 (decreased)
207 (increased)
240 (increased)
269 (increased)
260 (decreased)
263 (increased)
```

In this example, there are 7 measurements that are larger than the previous measurement.

**How many measurements are larger than the previous measurement?**

```elixir
defmodule Sonar do
  def parse_input(input) do
    input
    |> String.trim()
    |> String.split("\n")
    |> Enum.map(fn line ->
      {n, ""} = line |> String.trim() |> Integer.parse()
      n
    end)
  end

  def changes([]), do: []
  def changes([_]), do: []
  def changes([a, b | t]) when a < b, do: [:increased] ++ changes([b | t])
  def changes([a, b | t]) when a > b, do: [:decreased] ++ changes([b | t])
  def changes([a, b | t]) when a == b, do: [:same] ++ changes([b | t])

  def count(items) do
    items |> Enum.reduce(%{}, fn x, acc -> acc |> Map.update(x, 1, fn v -> v + 1 end) end)
  end
end

defmodule Sonar.Tests do
  use ExUnit.Case

  import Sonar

  test "example" do
    input = "
    199
    200
    208
    210
    200
    207
    240
    269
    260
    263
    "

    assert (parse_input(input) |> changes() |> count()).increased == 7
  end
end

ExUnit.run()
```

```elixir
day_1 = AoCInput.get_input(1)
```

```elixir
y = day_1 |> Sonar.parse_input() |> Enum.map(fn n -> -n end)

Vl.new(width: 750, height: 300)
|> Vl.data_from_series(
  x: 0..length(y),
  y: y
)
|> Vl.mark(:area)
|> Vl.encode_field(:x, "x", type: :quantitative)
|> Vl.encode_field(:y, "y", type: :quantitative)
```

```elixir
day_1 |> Sonar.parse_input() |> Sonar.changes() |> Sonar.count()
```

### Part 2

Considering every single measurement isn't as useful as you expected: there's just too much noise in the data.

Instead, consider sums of a three-measurement sliding window. Again considering the above example:

```
199  A      
200  A B    
208  A B C  
210    B C D
200  E   C D
207  E F   D
240  E F G  
269    F G H
260      G H
263        H
```

Start by comparing the first and second three-measurement windows. The measurements in the first window are marked A (199, 200, 208); their sum is 199 + 200 + 208 = 607. The second window is marked B (200, 208, 210); its sum is 618. The sum of measurements in the second window is larger than the sum of the first, so this first comparison increased.

Your goal now is to count the number of times the sum of measurements in this sliding window increases from the previous sum. So, compare A with B, then compare B with C, then C with D, and so on. Stop when there aren't enough measurements left to create a new three-measurement sum.

In the above example, the sum of each three-measurement window is as follows:

```
A: 607 (N/A - no previous sum)
B: 618 (increased)
C: 618 (no change)
D: 617 (decreased)
E: 647 (increased)
F: 716 (increased)
G: 769 (increased)
H: 792 (increased)
```

In this example, there are 5 sums that are larger than the previous sum.

Consider sums of a three-measurement sliding window. How many sums are larger than the previous sum?

```elixir
defmodule SlidingSonar do
  def windows(input) when length(input) < 3, do: []
  def windows([a, b, c | t]), do: [a + b + c] ++ windows([b, c | t])

  def changes(input), do: windows(input) |> Sonar.changes()
end

defmodule SlidingSonar.Tests do
  use ExUnit.Case

  import Sonar, only: [parse_input: 1, count: 1]
  import SlidingSonar

  test "example" do
    input = "
    199
    200
    208
    210
    200
    207
    240
    269
    260
    263
    "

    assert (parse_input(input) |> changes() |> count()).increased == 5
  end
end

ExUnit.run()
```

```elixir
day_1 |> Sonar.parse_input() |> SlidingSonar.changes() |> Sonar.count()
```
